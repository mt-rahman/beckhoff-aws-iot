<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.6">
  <POU Name="Get_Data" Id="{4915af13-8898-4876-ac87-d399fc3c78ec}" SpecialFunc="None">
    <Declaration><![CDATA[PROGRAM Get_Data
VAR
	fbSqlDatabase	: FB_SQLDatabaseEvt(sNetID := '', tTimeout := T#5S);
	fbSqlCommand	: FB_SQLCommandEvt(sNetID := '', tTimeout := T#5S);
	fbSqlCommand2	: FB_SQLCommandEvt(sNetID := '', tTimeout := T#5S);
	fbSqlResult		: FB_SQLResultEvt(sNetID:='', tTimeout   := T#5S); 
    aReadStruct		: ARRAY[1..50] OF ST_SelectStructData;
	nState			: INT;
	sCmd			: STRING(20000);
	sCmd2			: STRING(20000);
	
	count			: LINT;
	Data_Length		: UDINT;
	datetime		: STRING;
	Done			: BOOL;
	Deleted			: BOOL := TRUE;
	i 				: UDINT;
	
	TimeStr				: ARRAY[1..50] OF STRING;	
	Trailer_ID_Lead		: ARRAY[1..50] OF STRING;
	Trailer_ID_Rear		: ARRAY[1..50] OF STRING;
	Lat					: ARRAY[1..50] OF STRING;
	Lon					: ARRAY[1..50] OF STRING;
	Speed				: ARRAY[1..50] OF STRING;
	Overspeed			: ARRAY[1..50] OF STRING;
	Payload_Lead		: ARRAY[1..50] OF STRING;
	Payload_Rear		: ARRAY[1..50] OF STRING;
	Overload_Lead		: ARRAY[1..50] OF STRING;
	Overload_Rear		: ARRAY[1..50] OF STRING;
	Pressure_Lead		: ARRAY[1..50] OF STRING;
	Pressure_Rear		: ARRAY[1..50] OF STRING;
	Overpressure_Lead	: ARRAY[1..50] OF STRING;
	Overpressure_Rear	: ARRAY[1..50] OF STRING;
	EventCode			: ARRAY[1..50] OF STRING;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[(* get new data only if prev data sent to aws & saved to localdb *)
IF Main_SendData.Saved THEN
	nState 	:= 0;
	Done	:= FALSE;
	Deleted	:= FALSE;
END_IF

(* connect to localdb *)
IF NOT fbSqlDatabase.bConnected THEN
	fbSqlDatabase.Connect(1);
ELSE

	CASE nState OF
		
		0:
			IF fbSqlDatabase.CreateCmd(ADR(fbSqlCommand)) THEN
				IF fbSqlDatabase.bError THEN
					nState := 255; 
				ELSE
					nState := nState+1; 
				END_IF
			END_IF
			
		1:	
			
			IF Deleted THEN
				nState := nState+1;
			ELSE
				(* delete saved data from buffer *)
				sCmd2	:= 'DELETE FROM buffer_data WHERE datetime IN (SELECT datetime FROM buffer_data ORDER BY datetime ASC LIMIT ';
				sCmd2	:= CONCAT(sCmd2, UDINT_TO_STRING(Data_Length));
				sCmd2	:= CONCAT(sCmd2, ');');
				
				IF fbSQLCommand.Execute(ADR(sCmd2), SIZEOF(sCmd2)) THEN
					IF fbSQLCommand.bError THEN
						nState := 255; 
					ELSE
						nState := nState+1; 
						Deleted := TRUE;
					END_IF
				END_IF
			END_IF
			
		2:	(* count no of records in buffer *)
			sCmd2 := 'SELECT COUNT(*) FROM `buffer_data`';
			
			IF fbSQLCommand.ExecuteDataReturn(ADR(sCmd2), SIZEOF(sCmd2), ADR(fbSqlResult)) THEN
				IF fbSQLCommand.bError THEN
					nState := 255; 
				ELSE
					nState := nState+1; 
				END_IF
			END_IF
			
		3:	
			IF fbSqlResult.Read(0, 1, ADR(count), SIZEOF(count), TRUE, TRUE) THEN
				IF fbSqlResult.bError THEN
					nState := 255; 
				ELSE
					nState := nState+1; 
				END_IF
			END_IF
			
		4:
			IF fbSqlResult.Release() THEN
				IF fbSqlResult.bError THEN
					nState := 255;
				ELSE
					
					(* determine no of records to select *)
					IF count = 0 THEN
						Data_Length := 0;
						nState := 1;
					ELSIF count < 10 THEN
						Data_Length := 1;
						nState := nState+1;
					ELSIF count < 20 THEN
						Data_Length := 10;
						nState := nState+1;
					ELSIF count < 50 THEN
						Data_Length := 20;
						nState := nState+1;					
					ELSE
						Data_Length := 50;
						nState := nState+1;
					END_IF
					
				END_IF
				
			END_IF
			
		5:	(* get records from buffer *)
			sCmd2 := 'SELECT * FROM `buffer_data` ORDER BY datetime ASC LIMIT ';
			sCmd2 := CONCAT(sCmd2, UDINT_TO_STRING(Data_Length));
			
			IF fbSQLCommand.ExecuteDataReturn(ADR(sCmd2), SIZEOF(sCmd2), ADR(fbSqlResult)) THEN
				IF fbSQLCommand.bError THEN
					nState := 255; 
				ELSE
					nState := nState+1; 
				END_IF
			END_IF
			
		6:
			IF fbSqlResult.Read(0, Data_Length, ADR(aReadStruct), SIZEOF(aReadStruct), TRUE, TRUE) THEN
				IF fbSqlResult.bError THEN
					nState := 255; 
				ELSE
					nState := nState+1; 
				END_IF
			END_IF
			
		7:
			IF fbSqlResult.Release() THEN
				IF fbSqlResult.bError THEN
					nState := 255;
				ELSE
					nState := nState+1;
				END_IF
			END_IF
		
		8:
			(* parse query result *)
			FOR i:=1 TO Data_Length BY 1 DO
				TimeStr[i] 					:= aReadStruct[i].datetime;
				Trailer_ID_Lead[i] 			:= LINT_TO_STRING(aReadStruct[i].trailer_id_lead);
				Trailer_ID_Rear[i] 			:= LINT_TO_STRING(aReadStruct[i].trailer_id_rear);
				Lat[i] 						:= REAL_TO_STRING(aReadStruct[i].latitude);
				Lon[i] 						:= REAL_TO_STRING(aReadStruct[i].longitude);
				Speed[i] 					:= REAL_TO_STRING(aReadStruct[i].speed);
				Overspeed[i] 				:= LINT_TO_STRING(aReadStruct[i].overspeed);
				Payload_Lead[i]				:= REAL_TO_STRING(aReadStruct[i].payload_lead);
				Payload_Rear[i] 			:= REAL_TO_STRING(aReadStruct[i].payload_rear);
				Overload_Lead[i] 			:= LINT_TO_STRING(aReadStruct[i].overload_lead);
				Overload_Rear[i] 			:= LINT_TO_STRING(aReadStruct[i].overload_rear);
				Pressure_Lead[i] 			:= REAL_TO_STRING(aReadStruct[i].pressure_lead);
				Pressure_Rear[i] 			:= REAL_TO_STRING(aReadStruct[i].pressure_rear);
				Overpressure_Lead[i] 		:= REAL_TO_STRING(aReadStruct[i].overpressure_lead);
				Overpressure_Rear[i] 		:= REAL_TO_STRING(aReadStruct[i].overpressure_rear);
				EventCode[i]	 			:= REAL_TO_STRING(aReadStruct[i].event_code);
				
			END_FOR
			
			nState	:= nState + 1;
			
		9:
			Done	:= TRUE;
		
		255:
			nState	:= 0;	
			
	END_CASE

END_IF

]]></ST>
    </Implementation>
    <LineIds Name="Get_Data">
      <LineId Id="341" Count="3" />
      <LineId Id="736" Count="0" />
      <LineId Id="345" Count="16" />
      <LineId Id="738" Count="0" />
      <LineId Id="740" Count="14" />
      <LineId Id="757" Count="0" />
      <LineId Id="755" Count="1" />
      <LineId Id="739" Count="0" />
      <LineId Id="362" Count="25" />
      <LineId Id="389" Count="0" />
      <LineId Id="609" Count="0" />
      <LineId Id="687" Count="14" />
      <LineId Id="400" Count="2" />
      <LineId Id="561" Count="0" />
      <LineId Id="403" Count="31" />
      <LineId Id="445" Count="0" />
      <LineId Id="481" Count="0" />
      <LineId Id="622" Count="2" />
      <LineId Id="648" Count="0" />
      <LineId Id="625" Count="6" />
      <LineId Id="649" Count="0" />
      <LineId Id="632" Count="1" />
      <LineId Id="650" Count="2" />
      <LineId Id="644" Count="0" />
      <LineId Id="436" Count="0" />
      <LineId Id="506" Count="0" />
      <LineId Id="508" Count="2" />
      <LineId Id="480" Count="0" />
      <LineId Id="437" Count="7" />
      <LineId Id="75" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>